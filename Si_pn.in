go atlas simflags="-P 1"

###########################
### Section 1: Geometry ###
###########################

#Set a scale factor for all other 'spac' values
mesh  space.mult=1.0

#Define different mesh densities along the x axis 
x.mesh loc=0.00 spac=0.5
x.mesh loc=12.00 spac=0.5

#Define different mesh densities along the y axis
y.mesh loc=0.00 spac=0.1
y.mesh loc=5.00 spac=0.1

#Define material regions
region  num=1  material=silicon x.min=0.0 x.max=12.0 y.min=0.0 y.max=2.5
region  num=2  material=silicon x.min=0.0 x.max=12.0 y.min=2.5 y.max=5

#Define electrode names and positions
electr  name=anode  top
electr  name=cathode  bot

#Here we get to dope the material regions (1 and 2 from above)
doping     uniform reg=1  p.type conc=1.e16
doping     uniform reg=2  n.type conc=1.e16

###########################
### Section 2: Modeling ###
###########################

#so we tell ATLAS which models to apply
model   conmob  fldmob  srh  auger  bgn 

#And we tell it what values to solve each model for
output con.band val.band u.srh u.aug flowlines QFN QFP

#And tell it which numerical methods to use for solving 
#the specified models
method newton trap

#And then we solve for the initial conditions
solve  init

#Now we re-save our structure file with all of these solutions in it
save outf=Si_pn.str
#tonyplot Si_pn.str

#############################
### Section 3: Simulation ###
#############################

#We'll start with the dark current simulations
log outfile=Si_pn_dark.log
solve vcathode=0 vstep=5 vfinal=5 name=cathode outf=Si_pn_dark.str master
tonyplot Si_pn_dark.log
tonyplot Si_pn_dark.sts
tonyplot Si_pn_dark.stt

# Now we'll define a Single-wavelength source beam
# located 1 micron above the PD, with light angled 
# normal to the top of the PD.
# Wavelength default is 400nm.  We can change it later.
beam	num=1 x.origin=5.0 y.origin=-1.0 angle=90.0 wavelength=0.400

#now we simulate the current at the given wavelength for different
#cathode voltages, where the beam intensity is b1=1 Watt/cm^2
#this info is saved into the .log file
# !!! This is also where you get the answer to part 1 
solve	init
solve	vcathode=5
log outfile=Si_pn_5V_spec.log
solve	prev	b1=1 lambda=0.4
save outf=Si_pn_l1.str
solve	prev	b1=1 lambda=0.6
save outf=Si_pn_l2.str
solve	prev	b1=1 lambda=1.0
save outf=Si_pn_l3.str
tonyplot	Si_pn_5V_spec.log
tonyplot	Si_pn_l1.str
tonyplot	Si_pn_l2.str
tonyplot	Si_pn_l3.str

log outf=reset.log
solve	init
solve	vcathode = 5
log outf=Si_pn_I-Lambda.log
solve	prev	b1=1 lambda=0.4
solve	prev	b1=1 lambda=0.5
solve	prev	b1=1 lambda=0.6
solve	prev	b1=1 lambda=0.7
solve	prev	b1=1 lambda=0.8
solve	prev	b1=1 lambda=0.9
solve	prev	b1=1 lambda=1.0
tonyplot Si_pn_I-Lambda.log

save outf=Si_pn_final.str
tonyplot Si_pn_final.str

quit