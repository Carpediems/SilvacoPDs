go atlas simflags="-P 1"

###########################
### Section 1: Geometry ###
###########################

#Set a scale factor for all other 'spac' values
mesh  space.mult=1.0

#Define different mesh densities along the x axis 
x.mesh loc=0.00 spac=0.5
x.mesh loc=12.00 spac=0.5

#Define different mesh densities along the y axis
y.mesh loc=0.00 spac=0.1
y.mesh loc=5.00 spac=0.1

#Define material regions
region  num=1  material=silicon x.min=0.0 x.max=12.0 y.min=0.0 y.max=2.5
region  num=2  material=silicon x.min=0.0 x.max=12.0 y.min=2.5 y.max=5

#Define electrode names and positions
electr  name=anode  top
electr  name=cathode  bot

#Here we get to dope the material regions (1 and 2 from above)
doping     uniform reg=1  p.type conc=1.e16
doping     uniform reg=2  n.type conc=1.e16

###########################
### Section 2: Modeling ###
###########################

#so we tell ATLAS which models to apply
model   conmob  fldmob  srh  auger  bgn 

#And we tell it what values to solve each model for
output con.band val.band u.srh u.aug flowlines QFN QFP

#And tell it which numerical methods to use for solving 
#the specified models
method newton trap

#And then we solve for the initial conditions
solve  init

#Now we re-save our structure file with all of these solutions in it
save outf=Si_pn.str
#tonyplot Si_pn.str

#############################
### Section 3: Simulation ###
#############################

#Problem 2:
#We'll start with the dark current simulations
#since no light sources have yet been defined
log outfile=Si_pn_dark.log
solve vcathode=0 vstep=1 vfinal=5 name=cathode outf=Si_pn_dark.str master

#this statement stops adding anything else to the log
#until we issue a new "log" command
log off

tonyplot Si_pn_dark.log

#Problem 4:
#The "master" command saves the structure at each incremental voltage
#into the following files, with each increment in voltage corresponding
#to a subsequent final letter in the file name.  i.e. 'r', 's', 't'
#tonyplot Si_pn_dark.str
#tonyplot Si_pn_dark.sts
#tonyplot Si_pn_dark.stt
#tonyplot Si_pn_dark.stu
#tonyplot Si_pn_dark.stv
#tonyplot Si_pn_dark.stw
#tonyplot Si_pn_dark.stx
#tonyplot Si_pn_dark.sty

# Now we'll define a Single-wavelength source beam
# located 1 micron above the PD, with light angled 
# normal to the top of the PD.
# Wavelength default is 400nm.  We can change it later.
beam	num=1 x.origin=5.0 y.origin=-1.0 angle=90.0 wavelength=0.400

#Problem 1:
#Now we simulate the response at the given wavelength for 
#cathode voltage of 5V, where the beam intensity is b1=1 Watt/cm^2
#this info is saved into the .log file, and we can plot the
#photogeneration rate from the individual .str files 
solve	init
solve	vcathode=5
log outfile=Si_pn_5V_spec.log
solve	prev	b1=1 lambda=0.4
save outf=Si_pn_l1.str
solve	prev	b1=1 lambda=0.6
save outf=Si_pn_l2.str
solve	prev	b1=1 lambda=1.0
save outf=Si_pn_l3.str
log off

#tonyplot	Si_pn_5V_spec.log
tonyplot	Si_pn_l1.str
tonyplot	Si_pn_l2.str
tonyplot	Si_pn_l3.str

#Problem 3
#now we reset the system at 5V, and simulate the response
#to each wavelength
solve	init
solve	vcathode = 5
log outf=Si_pn_I-Lambda.log
solve	prev	b1=1 lambda=0.4
solve	prev	b1=1 lambda=0.5
solve	prev	b1=1 lambda=0.6
solve	prev	b1=1 lambda=0.7
solve	prev	b1=1 lambda=0.8
solve	prev	b1=1 lambda=0.9
solve	prev	b1=1 lambda=1.0
log off
tonyplot Si_pn_I-Lambda.log

save outf=Si_pn_final.str
tonyplot Si_pn_final.str

quit